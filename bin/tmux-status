#!/usr/local/bin/zsh

#
# Outputs the left and right statusline configurations.
#

# validate input arguments
if [[ "$#" -ne 2 || (
  "$1" != "left" && "$1" != "right" && "$1" != "center" && "$1" != "center-current"
  ) ]]; then
  echo "Usage: tmux-status [left/right/center] [width]"
  exit 1
fi

# validate width argument
if ! [[ $2 =~ ^-?[0-9]+$ ]]; then
 echo "Width argument must be an integer."
 exit 1
fi

SIDE="$1"
WIDTH="$2"

# determine display size
SIZE="max"
if (( $WIDTH < 90 )); then SIZE="min";
elif (( $WIDTH < 121 )); then SIZE="med"; fi

# setup colors
COL1="#[fg=colour236,bg=colour103]"
COL1_2="#[fg=colour103,bg=colour239,nobold,nounderscore,noitalics]"
COL1_3="#[fg=colour103,bg=colour236,nobold,nounderscore,noitalics]"
COL2="#[fg=colour253,bg=colour239]"
COL2_3="#[fg=colour239,bg=colour236,nobold,nounderscore,noitalics]"
COL3="#[fg=colour244,bg=colour236]"
COL3_BB_10="#[fg=colour160,bg=colour236]"
COL3_BB_20="#[fg=colour124,bg=colour236]"
COL3_BC_10="#[fg=colour166,bg=colour236]"
COL3_BC_20="#[fg=colour130,bg=colour236]"
COL3_2="#[fg=colour236,bg=colour239,nobold,nounderscore,noitalics]"
COL3_1="#[fg=colour236,bg=colour103,nobold,nounderscore,noitalics]"

function left {
  SEP="⮁"
  FIRST="#S $SEP #F"
  SECOND="$(ssid | sed s/\#//g)"
  THIRD=""
  if [[ -z "$SECOND" ]]; then
    SECOND="[no network]"
  else
    SECOND="[$SECOND] $(local-ip en7 2>/dev/null || local-ip || echo "127.0.0.1")"
    THIRD="$(ip)"
  fi
  SEP="⮀"
  case $SIZE in
    min) echo "$COL1 $FIRST $COL1_3$SEP$COL3" ;;
    med) echo "$COL1 $FIRST $COL1_2$SEP$COL2 $THIRD $COL2_3$SEP$COL3" ;;
    max) echo "$COL1 $FIRST $COL1_2$SEP$COL2 $SECOND $COL2_3$SEP$COL3 $THIRD" ;;
  esac
}

function battery {
  BATTERY_PERCENT="${$(battery-percent): :-1}" # strip trailing %
  BATTERY_STATE="$(battery-state)"
  BATTERY_COLOR=""
  # <= 20% colors
  if [ "$BATTERY_PERCENT" -le 20 ]; then
    if [ "$BATTERY_STATE" = "charging" ]; then
      BATTERY_COLOR=$COL3_BC_20
    else
      BATTERY_COLOR=$COL3_BB_20
    fi
  fi
  # <= 10% colors
  if [ "$BATTERY_PERCENT" -le 10 ]; then
    if [ "$BATTERY_STATE" = "charging" ]; then
      BATTERY_COLOR=$COL3_BC_10
    else
      BATTERY_COLOR=$COL3_BB_10
    fi
  fi
  echo "$BATTERY_COLOR$BATTERY_PERCENT%"
}

function right {
  SEP="⮃"
  FIRST="$(echo $(date +'%l:%M%p') | tr '[:upper:]' '[:lower:]')"
  SECOND="$(date +'%a %-d') $SEP $(date +'%b %Y')"
  THIRD="$(cpu)c $(memory)m $(battery)b"
  SEP="⮂"
  case $SIZE in
    min) echo "$COL1_3$SEP$COL1 $FIRST " ;;
    med) echo "$COL2_3$SEP$COL2 $THIRD $COL1_2$SEP$COL1 $FIRST " ;;
    max) echo "$COL3 $THIRD $COL2_3$SEP$COL2 $SECOND $COL1_2$SEP$COL1 $FIRST " ;;
  esac
}

if [ "$SIDE" = "left" ]; then
  left
elif [ "$SIDE" = "right" ]; then
  right
elif [ "$SIDE" = "center" ]; then
  echo "${COL3_2}⮀${COL2} #I #W ${COL2_3}⮀"
elif [ "$SIDE" = "center-current" ]; then
  echo "${COL3_1}⮀${COL1} #I #W ${COL1_3}⮀"
fi
